
#-----------------------------------------------------------------------
#                         Initial Comments
#-----------------------------------------------------------------------
# Name
# Date
# Model version

# Units
#------
# Body weight (kg)
# Drug amount (umol)
# Volumes (L)
# Time (hr)


#-----------------------------------------------------------------------
# 			            Initialize Parameters 
#               (plus params for added compartments)
#-----------------------------------------------------------------------
BW = 60; # Kg
QCC = 15; # Cardiac output L/hr/kg^0.75

VSC = 0.185; # Slowly perfused compartment L/BW^0.75
VRC = 0.0765; # Rapidly L/BW^0.75
VBLC = 0.1; # Blood volume L/BW^0.75

QSC = 0.3; # Slowly perfused
QRC = 0.7; # Rapidly perfused

CLC = 0.1; # L/hr/kg^0.75

PR = 0.6; # Parition coefficients (Rapid)
PS = 0.2; # Parition coefficients (Slow)

MW = 300; # ug/umol


#-----------------------------------------------------------------------
#                   Parameters for Exposure/Dose
#-----------------------------------------------------------------------
OralDose;	# mg/kg dose of drug
OralDur = 0.001; # Duration of dosing
ODose;		# Dose umol/hr
fa = 1;		# Fraction absorbed


#-----------------------------------------------------------------------
#                   Defined Scaled Parameters
#				(Anything that gets calculated in "Dynamics")
#-----------------------------------------------------------------------
QC; QR; QS;
VR; VS; VBL;
CL;


#-----------------------------------------------------------------------
#                   Calibration Parameters
#-----------------------------------------------------------------------



# Population Mean Parameters (M_...)
#-----------------------------------



# Population Variance Parameters (Var_...)
#-----------------------------------------


#-----------------------------------------------------------------------
#                          Data Error (Verr_...)
# 				Define here if you are using an error distribution, 
# 					rather than error at specific time points.
#-----------------------------------------------------------------------



#-----------------------------------------------------------------------
#                 State Variables (Dependent Variables in ODE)
#-----------------------------------------------------------------------
States = { # Separate each state with a comma
AO,
AELIM,

AS,
AR,
ABL,

};



#-----------------------------------------------------------------------
#                    Inputs (Switches for Exposures)
#-----------------------------------------------------------------------
Inputs = { # Separate each input with a comma
OralExp

};



#-----------------------------------------------------------------------
#                       Outputs from the Model
# 				Anything in CalcOutputs must go here
#-----------------------------------------------------------------------
Outputs = { # Separate with comma
# Transform concentrations
#--------------------------
CR, CS, CV,
CR_mgL,
CS_mgL,
CBL_mg,


# Calculate Mass Balance
#------------------------
ACC,
NetIn,
BalErr,

};




#-----------------------------------------------------------------------
#                       Initialize the Model
# 		Initialize paramaters, set initial conditions, etc.
#-----------------------------------------------------------------------
Initialize { # Separate with semicolon
VR = VRC*pow(BW, 0.75); # VRC*BW^0.75
VS = VSC*pow(BW, 0.75);
VBL = VBLC*pow(BW, 0.75);

QC = QCC*pow(BW, 0.75);
QR = QRC*QC;
QS = QSC*QC;

CL = CLC*pow(BW, 0.75);

ODose = (fa*OralDose*BW/OralDur)*(1000./MW); # mg/kg --> umol/hr



};



#-----------------------------------------------------------------------
#                       The Model (Actual ODEs)
#-----------------------------------------------------------------------
Dynamics { # Separate with semicolon

# Define concentrations or other independent variables used in ODEs
#------------------------------------------------------------------
CR = AR/VR; CVR = CR/PR;
CS = AS/VS; CVS = CS/PS;
CA = ABL/VBL;
CV = (QR*CVR + QS*CVS)/QC;


# Acutal ODE's
#--------------
dt(AO) = OralExp*ODose;
dt(AELIM) = CL*CR;

dt(AR) = QR*(CA - CVR) - dt(AELIM);
dt(AS) = dt(AO) + QS*(CA - CVS);
dt(ABL) = QC*(CV - CA);



};


#-----------------------------------------------------------------------
#                       Calculate the Model Outputs
#		(Any model output calculation must go here first)
#-----------------------------------------------------------------------
CalcOutputs { # Separate with semicolon

# Transform concentrations
#--------------------------
CR_mgL = CR*MW/1000;
CS_mgL = CS*MW/1000;
CBL_mg = CV*MW/1000;


# Calculate Mass Balance
#------------------------
ACC = AR + AS + ABL;
NetIn = AO - AELIM;
BalErr = NetIn - ACC;

}
End.

