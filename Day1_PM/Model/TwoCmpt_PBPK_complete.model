
#-----------------------------------------------------------------------
#                         Initial Comments
#-----------------------------------------------------------------------
# Name
# Date
# Model version


#-----------------------------------------------------------------------
# 			            Initialize Parameters 
#               (plus params for added compartments)
#-----------------------------------------------------------------------
QCC = 15.; # Cardiac output L/kg
BW = 60.; # BW in kg

VSC = 0.185;            # Slowly Perfused L/BW^0.75
VRC = 0.0765;  			# Rapidly Perfused L/BW^0.75
VBLC = 0.1;				# Blood volume l/BW^0.75

QSC = 0.3;          # Slowly Perfused percentage of cardiac output
QRC = 0.7; 			# Rapidly Perfused percentage of cardiac output

CLC = 0.1;			# Clearance rate L/hr/kg^0.75

PR = 0.6;
PS = 0.2;

MW = 300;		# Molecular weight of drug (ug/umol)



#-----------------------------------------------------------------------
#                   Parameters for Exposure/Dose
#-----------------------------------------------------------------------
OralDose;		# Administered dose (mg/kg)
OralDur = 0.001; # Duration of oral dose (hr)
ODose;     # Dose of chemical (muM/hr)
fa = 1;		# Fraction absorbed

#-----------------------------------------------------------------------
#                   Defined Scaled Parameters
#				(Anything that gets calculated in "Dynamics")
#-----------------------------------------------------------------------
VR; VS; VBL;
QC; QR; QS;
CL;
#CR; CS; CA;


#-----------------------------------------------------------------------
#                   Calibration Parameters
#-----------------------------------------------------------------------



# Population Mean Parameters (M_...)
#-----------------------------------



# Population Variance Parameters (Var_...)
#-----------------------------------------


#-----------------------------------------------------------------------
#                          Data Error (Verr_...)
# 				Define here if you are using an error distribution, 
# 					rather than error at specific time points.
#-----------------------------------------------------------------------



#-----------------------------------------------------------------------
#                 State Variables (Dependent Variables in ODE)
#-----------------------------------------------------------------------
States = { # Separate each state with a comma
AO, # Oral dosing to slowly perfused
AELIM, # elimination rate from rapidly perfused

AS, # Slowly perfused compartment
AR, # Rapidly perfused compartment
ABL, # Blood amount

};



#-----------------------------------------------------------------------
#                    Inputs (Switches for Exposures)
#-----------------------------------------------------------------------
Inputs = { # Separate each input with a comma
OralExp,

};



#-----------------------------------------------------------------------
#                       Outputs from the Model
# 				Anything in CalcOutputs must go here
#-----------------------------------------------------------------------
Outputs = { # Separate with comma
CR, CS, CA,
CR_mgL, CS_mgL, CBL_mgL,

# Mass Balances
ACC,  NetIn,  BalErr,
};




#-----------------------------------------------------------------------
#                       Initialize the Model
# 		Initialize paramaters, set initial conditions, etc.
#-----------------------------------------------------------------------
Initialize { # Separate with semicolon
ODose  = (fa*OralDose*BW/OralDur)*(1000./MW); #umol/hr dosing

VR = VRC*pow(BW, 0.75); 
VS = VSC*pow(BW, 0.75);
VBL = VBLC*pow(BW, 0.75);
QC = QCC*pow(BW, 0.75);
QR = QRC*QC; 
QS = QSC*QC;

CL = CLC*pow(BW, 0.75);

};



#-----------------------------------------------------------------------
#                       The Model (Actual ODEs)
#-----------------------------------------------------------------------
Dynamics { # Separate with semicolon

# Define concentrations or other independent variables used in ODEs
#------------------------------------------------------------------
CR = AR/VR;      # Rapidly Perfused
CVR = CR/PR;
CS = AS/VS;      # Slowly Perfused
CVS = CS/PS;

CA = ABL/VBL;   # APAP Blood
CV = (QR*CVR + QS*CVS)/QC; # Pooled venous blood concentration



# Acutal ODE's
#--------------
dt(AO) = OralExp*ODose;
dt(AELIM) = CL*CR;

dt(AR) = QR*(CA - CVR) - dt(AELIM); # Elimiation in rapidly perfused
dt(AS) = dt(AO) + QS*(CA - CVS);
dt(ABL) = QC*(CV - CA);


};


#-----------------------------------------------------------------------
#                       Calculate the Model Outputs
#		(Any model output calculation must go here first)
#-----------------------------------------------------------------------
CalcOutputs { # Separate with semicolon

# Transform concentrations
#--------------------------
CR_mgL = CR*1000/MW;
CS_mgL = CS*1000/MW;
CBL_mgL = CA*1000/MW;


# Calculate Mass Balance
#------------------------
ACC = AR + AS + ABL;
NetIn = AO - AELIM;
BalErr = NetIn - ACC;
}

